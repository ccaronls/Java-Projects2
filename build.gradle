// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlin_version = '2.0.20'
        google_json_version = '2.10.1'
        ksp_version = '2.0.20-1.0.25'
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url 'https://www.jitpack.io'
            name 'jitpack'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.8.0' apply false
    id 'org.jetbrains.kotlin.multiplatform' version '2.0.20' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.0.20' apply false
    id 'com.google.devtools.ksp' version "${ksp_version}" apply false
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        incremental = true
    }
}


allprojects {

    repositories {
        mavenCentral()
        google()
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
        mavenLocal()
        maven {
            url 'https://www.jitpack.io'
            name 'jitpack'
        }
    }

    ext {
        junit = "junit:junit:4.12"
    }

    plugins.withType(JavaPlugin) {
        dependencies {
            testImplementation "$junit"
//  Figure out how to allow for this in this file and not in every project
//            evaluationDependsOn(':GameLibrary')
//            testCompile files(project(':GameLibrary').sourceSets.test.output)
        }

        java {
            sourceCompatibility = JavaVersion.toVersion(project.sourceCompatibility)
            targetCompatibility = JavaVersion.toVersion(project.targetCompatibility)
        }

        dependencies {
            implementation "com.google.code.gson:gson:${google_json_version}"
        }
    }

    plugins.withId("com.android.library") {
        android {
            namespace = "cc.lib.android"
            defaultConfig {
                minSdkVersion project.myMinSdkVersion
                targetSdkVersion project.myTargetSdkVersion
                testInstrumentationRunner project.androidTestRunner
            }
            compileOptions {
                sourceCompatibility project.sourceCompatibility
                targetCompatibility project.targetCompatibility
            }
            compileSdk project.myCompileSdkVersion.toInteger()
            buildFeatures {
                buildConfig = true
            }
        }

        dependencies {
            implementation 'androidx.annotation:annotation:1.8.2'
            implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
            implementation "androidx.viewpager:viewpager:1.0.0"
            implementation 'androidx.recyclerview:recyclerview:1.3.2'
            implementation 'androidx.appcompat:appcompat:1.7.0'
            implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
            implementation "com.google.code.gson:gson:${google_json_version}"

            testImplementation "$junit"

            // Core library
            androidTestImplementation 'androidx.test:core:1.4.0'

            // AndroidJUnitRunner and JUnit Rules
            androidTestImplementation 'androidx.test:runner:1.4.0'
            androidTestImplementation 'androidx.test:rules:1.4.0'
        }

    }

    plugins.withId("com.android.application") {
        android {
            defaultConfig {
                minSdkVersion project.myMinSdkVersion
                targetSdkVersion project.myTargetSdkVersion
                testInstrumentationRunner project.androidTestRunner
                multiDexEnabled true
                vectorDrawables.useSupportLibrary true
            }

            compileOptions {
                coreLibraryDesugaringEnabled true
                sourceCompatibility project.sourceCompatibility
                targetCompatibility project.targetCompatibility
            }

            compileSdk project.myCompileSdkVersion.toInteger()
            //buildToolsVersion project.myBuildToolsVersion

            signingConfigs {
                general {
                    storeFile file("common/android.jks")
                    storePassword sebi_storePassword
                    keyAlias sebi_keyAlias
                    keyPassword sebi_keyPassword
                }
            }

            buildTypes {

                /**
                 * By default, Android Studio configures the release build type to enable code
                 * shrinking, using minifyEnabled, and specifies the Proguard settings file.
                 */
                debug {
                    debuggable true
                    minifyEnabled false
                    signingConfig signingConfigs.general
                }

                release {
                    minifyEnabled true // Enables code shrinking for the release build type.
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'common/proguard.txt'
                    signingConfig signingConfigs.general
                }
            }

            buildFeatures {
                dataBinding true
                viewBinding true
            }

            sourceSets {
                main {
                    assets.srcDirs = ['assets']
                }
            }

            buildFeatures {
                buildConfig = true
            }

        }

        dependencies {
            implementation 'androidx.annotation:annotation:1.8.2'
            implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
            implementation "androidx.viewpager:viewpager:1.0.0"
            implementation 'androidx.recyclerview:recyclerview:1.3.2'
            implementation 'androidx.appcompat:appcompat:1.7.0'
            implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
            implementation 'androidx.fragment:fragment-ktx:1.8.3'
            implementation "com.google.code.gson:gson:${google_json_version}"

            coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'

            testImplementation "$junit"

            // Core library
            androidTestImplementation 'androidx.test:core:1.4.0'

            // AndroidJUnitRunner and JUnit Rules
            androidTestImplementation 'androidx.test:runner:1.4.0'
            androidTestImplementation 'androidx.test:rules:1.4.0'

            // This thing is awesome
            debugImplementation 'com.squareup.leakcanary:leakcanary-android:+'
        }

        configurations {
            //debugImplementation.exclude group: "junit", module: "junit"
        }
    }

    plugins.withId("kotlin-android") {
        android {
            kotlinOptions {
                jvmTarget = project.sourceCompatibility
            }
        }
        dependencies {
            implementation 'androidx.core:core-ktx:1.13.1'
            implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
            implementation "com.google.code.gson:gson:${google_json_version}"
        }
    }

    plugins.withId("java-library") {
        dependencies {
            implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
        }

        java {
            sourceCompatibility = JavaVersion.toVersion(project.sourceCompatibility)
            targetCompatibility = JavaVersion.toVersion(project.targetCompatibility)
        }
    }

    tasks.withType(JavaExec) {
        enableAssertions = true
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

