options {
    static=false;
}

PARSER_BEGIN(Evaluator)

package cc.app.fractal.evaluator;

import cc.app.fractal.*;

public class Evaluator {

    private final ComplexNumber Z0 = new ComplexNumber();
    private final ComplexNumber Zi = new ComplexNumber();
    private final ComplexNumber t0 = new ComplexNumber();
    private final ComplexNumber t1 = new ComplexNumber();

    private Node root;

    public Evaluator() {
        this(new java.io.StringReader(""));
    }

    public void parse(String expression) throws Exception {
        System.out.println("Parse expression: " + expression);
        ReInit(new java.io.StringReader(expression));
        root = S();
    }

    public void reset(ComplexNumber Z0) {
        this.Z0.copy(Z0);
        Zi.copy(Z0);
    }

    public ComplexNumber evaluate() {
        if (root != null)
            Zi.copy(evaluateR(root));
        return Zi;
    }

    private ComplexNumber evaluateR(Node n) {
        switch (n.type) {
        case TYPE_ADD:
            return evaluateR(n.left).add(evaluateR(n.right), t0);
        case TYPE_SUB:
            return evaluateR(n.left).sub(evaluateR(n.right), t0);
        case TYPE_MULT:
            return evaluateR(n.left).multiply(evaluateR(n.right), t0);
        case TYPE_DIV:
            return evaluateR(n.left).divide(evaluateR(n.right), t0);
        case TYPE_POWI:
            return evaluateR(n.left).powi(n.right.numi, t0);
        case TYPE_POWD:
            return evaluateR(n.left).powd(n.right.numd, t0);
        case TYPE_POWC:
            return evaluateR(n.left).powc(evaluateR(n.right), t0);
        case TYPE_SQRT:
            return evaluateR(n.left).sqrt(t0);
        case TYPE_SIN:
            return evaluateR(n.left).sine(t0);
        case TYPE_COS:
            return evaluateR(n.left).cosine(t0);
        case TYPE_TAN:
            return evaluateR(n.left).tangent(t0);
        case TYPE_SINH:
            return evaluateR(n.left).sineh(t0);
        case TYPE_COSH:
            return evaluateR(n.left).cosineh(t0);
        case TYPE_LN:
            return evaluateR(n.left).ln(t0);
        case TYPE_EXP:
            return evaluateR(n.left).exp(t0);
        case TYPE_CONSTANT:
            return n.numc;
        case TYPE_Z0:
            return Z0;
        case TYPE_Zi:
            return Zi;
        case TYPE_NEGATE:
            return evaluateR(n.left).negate(t0);
        }
        throw new RuntimeException("Unhandled type");
    }

    public void debugDump() {
        debugDumpR(0, root);
    }

    private void debugDumpR(int level, Node n) {
        if (n == null)
            return;

        for (int i=0; i<level; i++)
            System.out.print("  ");

        System.out.print(n.type.name());
        switch (n.type) {
        case TYPE_CONSTANT:
            System.out.println(" " + n.stringValue); break;
            
        case TYPE_Z0:
            System.out.println(" Z0"); break;
            
        case TYPE_Zi:
            System.out.println(" Zi"); break;
            
        default:
            System.out.println();
        }
        debugDumpR(level+1, n.left);
        debugDumpR(level+1, n.right);
    }

// We really dont need this, Z0 and Zi are an extension of CONSTANT
// Should be something like: FUNC, NUM, COMPLEX_NUM only
    private enum Type {
        TYPE_ADD, // complex + complex
        TYPE_SUB, // complex - complex
        TYPE_MULT, // complex * complex
        TYPE_DIV,  // complex / complex
        TYPE_CONSTANT, // Complex constant [a+bi]
        TYPE_POWI, // complex ^ int
        TYPE_POWD, // complex ^ double
        TYPE_POWC, // complex ^ complex
        TYPE_SQRT, // sqrt (complex)
        TYPE_SIN, // sin (complex)
        TYPE_COS, // cos (complex)
        TYPE_TAN, // tan (complex)
        TYPE_SINH, // sinh (complex)
        TYPE_COSH, // cosh (complex)
        TYPE_LN,  // ln (complex)
        TYPE_EXP, // exp (complex)
        TYPE_Z0,
        TYPE_Zi,
        TYPE_NEGATE, // -constant
    }
    
    // TODO: Merge with the functions of Type, make type one of constant or func, move
    //   this enum to an external file with the evaluators as abstract function.
    //   Should be able to add functions with out running javacc on this file.
    private enum Function {
        sqrt(Type.TYPE_SQRT),
        sin(Type.TYPE_SIN),
        cos(Type.TYPE_COS),
        tan(Type.TYPE_TAN),
        sinh(Type.TYPE_SINH),
        cosh(Type.TYPE_COSH),
        ln(Type.TYPE_LN),
        exp(Type.TYPE_EXP),
        ;
        
        Function(Type type) {
            this.type = type;
        }
        
        final Type type;
    }
    
    private Type lookupFunc(String name) {
        try {
            return Function.valueOf(name).type;
        } catch (Exception e) {
            throw new RuntimeException("Unknown function '" + name + "' must be one of : " + java.util.Arrays.asList(Function.values()));
        }
    }

    final int CONSTANT_COMPLEX = 0;
    final int CONSTANT_DOUBLE  = 1;
    final int CONSTANT_INT     = 2;

    private class Node {
        Type type;
        ComplexNumber numc;
        int numi;
        double numd;
        int constantType = -1;
        String stringValue;

        Node(Type type, Node left, Node right) {
            this.type = type;
            this.left = left;
            this.right = right;
        }
/*
        Node(Node left, String power) {
            try
            {
              this.poweri = Integer.parseInt(power);
              this.type = Type.TYPE_POW;
            } catch (NumberFormatException e)
            {
              this.powerd = Double.parseDouble(power);
              this.type = Type.TYPE_DPOW;
            }
            this.left = left;
        }
  */      
        Node(double constant, String value) {
            type = Type.TYPE_CONSTANT;
            constantType = CONSTANT_DOUBLE;
            numd = constant;
            numc = new ComplexNumber(numd, 0);
            this.stringValue = value == null ? ("" + numd) : value;
        }
  
        Node(String constant) {
            stringValue = constant;
            type = Type.TYPE_CONSTANT;
            try {
               numi = Integer.parseInt(constant);
               numd = numi;
               constantType = CONSTANT_INT;
            } catch (NumberFormatException e) {
               numd = Double.parseDouble(constant);
               constantType = CONSTANT_DOUBLE;
            }
            numc = new ComplexNumber(numd, 0);
        }

        Node(Type type) {
            this.type = type;
        }

        Node(ComplexNumber constant) {
            this.type = Type.TYPE_CONSTANT;
            this.constantType = CONSTANT_COMPLEX;
            this.numc = constant;
            stringValue = constant.toString();
        }

        Node left, right;

    }
}

PARSER_END(Evaluator)

SKIP:  { 
    " " 
  | "\t" 
  | "\n" 
  | "\r"     
}

TOKEN: { 
    "(" 
  | ")" 
  | "+"
  | "-" 
  | "*" 
  | "/"  
  | "^"
  | <FUNC: ((["a"-"h"]|["j"-"z"])(["a"-"z"])+)>
  | <NUM: (("-")?("0"|((["1"-"9"])(["0"-"9"])*))("."(["0"-"9"])+)?)>
  | <DOUBLE: (("-")?("0"|((["1"-"9"])(["0"-"9"])*))("."(["0"-"9"])+))>
  | <INT: (("-")?("0"|((["1"-"9"])(["0"-"9"])*)))>
}

Node S(): {Node n;}
{
  n=E() <EOF> {return n;}
}

Node E(): {Node n1, n2; Type t;}
{
  n1=T() ( ( "+" {t=Type.TYPE_ADD;} | "-" {t=Type.TYPE_SUB;} ) 
           n2=T() {n1 = new Node(t, n1, n2); } )* { return n1; }
}

Node T(): {Node n1, n2; Type t;}
{
  n1=P() ( ( "*" {t=Type.TYPE_MULT;} | "/" {t=Type.TYPE_DIV;} )
           n2=P() {n1 = new Node(t, n1, n2); } )* {return n1;}
}

Node P(): {Node n1, n2; }
{
  n1=F() ( "^" n2=F() {
     if (n2.type == Type.TYPE_CONSTANT) {
        if (n2.constantType == CONSTANT_INT) {
           n1 = new Node(Type.TYPE_POWI, n1, n2);
        } else if (n2.constantType == CONSTANT_DOUBLE) {
           n1 = new Node(Type.TYPE_POWD, n1, n2);
        } else {
           n1 = new Node(Type.TYPE_POWC, n1, n2);
        }
     } else {
        n1 = new Node(Type.TYPE_POWC, n1, n2);
     }
  } 
  )? {return n1;}
}

Node F(): {Token n0, n1; Node n; Type f; }
{
  "[" n0=<NUM> (","|"+") n1=<NUM> ("i")? "]"  {return new Node(new ComplexNumber(Double.parseDouble(n0.image), Double.parseDouble(n1.image)));}
|
  "(" n=E() ")" {return n;}
| 
  "Z0" { return new Node(Type.TYPE_Z0); }
|
  "Z" { return new Node(Type.TYPE_Zi); }
|
  "E"  { return new Node(Math.E, "E"); }
| 
  "PI" { return new Node(Math.PI, "PI"); }
| 
  "X"  { return new Node(Zi.getReal(), "X"); }
|
  "Y"  { return new Node(Zi.getImag(), "Y"); }
|
  "C"  { return new Node(Type.TYPE_Z0); }
|
  "A"  { return new Node(Z0.getReal(), "A"); }
|
  "B"  { return new Node(Z0.getImag(), "B"); }
|
  n0=<NUM>  { return new Node(n0.image); }
|
  n0=<FUNC> { f=lookupFunc(n0.image); } "(" n=E() ")" { return new Node(f, n, null); }
|
  "-" n=F() { return new Node(Type.TYPE_NEGATE, n, null); }
}

