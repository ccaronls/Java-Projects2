apply plugin: 'com.android.application'

android {
    compileSdkVersion project.myCompileSdkVersion.toInteger()
    buildToolsVersion project.myBuildToolsVersion

    defaultConfig {
        applicationId "cc.game.android.risk"
        minSdkVersion project.myMinSdkVersion
        targetSdkVersion project.myTargetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner project.androidTestRunner
    }

    signingConfigs {
        general {
            storeFile file(project.storeFile)
            storePassword project.storePassword
            keyAlias project.keyAlias
            keyPassword project.keyPassword
        }
    }

    compileOptions {
        sourceCompatibility project.sourceCompatibility
        targetCompatibility project.targetCompatibility
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.general
        }

        release {
            minifyEnabled true // Enables code shrinking for the release build type.
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../common/proguard.txt'
            signingConfig signingConfigs.general
        }

    }

    applicationVariants.all {
        variant -> variant.outputs.all {
            outputFileName = "risk-${variant.buildType.name}-${android.defaultConfig.versionName}.${android.defaultConfig.versionCode}.apk"
        }
    }

    sourceSets {
        main {
            assets.srcDirs = [ 'assets' ]
        }
    }

    aaptOptions { cruncherEnabled = false }
}

dependencies {
    implementation project(':AndroidGameLibrary')
    implementation project(':AndroidMultiplayerGameLibrary')
    implementation project(path: ':GameLibrary', configuration: 'androidLib')
    implementation fileTree(dir: "libs", include: ["*.jar"])
}
